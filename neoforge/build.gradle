plugins {
    id 'multiloader-loader'
    id 'net.neoforged.gradle.userdev' version '7.0.145'
    id 'com.modrinth.minotaur' version '2.+'
    id 'net.darkhax.curseforgegradle' version '1.+'
}

// Automatically enable neoforge AccessTransformers if the file exists
// This location is hardcoded in FML and can not be changed.
// https://github.com/neoforged/FancyModLoader/blob/a952595eaaddd571fbc53f43847680b00894e0c1/loader/src/main/java/net/neoforged/fml/loading/moddiscovery/ModFile.java#L118
def at = file('src/main/resources/META-INF/accesstransformer.cfg')
if (at.exists()) {
    minecraft.accessTransformers.file at
}

sourceSets {
    test {
        runs {
            modIdentifier 'curiostest'
        }
    }
}

runs {
    configureEach {
        modSource project.sourceSets.main
        modSource project.sourceSets.test
    }
    client {
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
    }
    server {
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
        programArgument '--nogui'
    }

    gameTestServer {
        systemProperty 'neoforge.enabledGameTestNamespaces', project.mod_id
    }

    data {
        programArguments.addAll '--mod', 'curiostest', '--all', '--output', file('src/generated/resources/').getAbsolutePath(), '--existing', file('src/main/resources/').getAbsolutePath()
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven {
        url "https://www.cursemaven.com"
    }
    maven {
        // location of the maven that hosts JEI files since January 2023
        name = "Jared's maven"
        url = "https://maven.blamejared.com/"
    }
    maven {
        // location of a maven mirror for JEI files, as a fallback
        name = "ModMaven"
        url = "https://modmaven.dev"
    }
}

dependencies {
    implementation "net.neoforged:neoforge:${neoforge_version}"

    // compile against the JEI API but do not include it at runtime
    compileOnly "mezz.jei:jei-${minecraft_version}-neoforge-api:${jei_version}"
    runtimeOnly "mezz.jei:jei-${minecraft_version}-neoforge:${jei_version}"

    testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.8.2'

    implementation "curse.maven:octo-lib-916747:5447735"
    implementation "curse.maven:relics-mod-445274:5532603"
    implementation "curse.maven:caelus-308989:5442975"
    implementation "curse.maven:geckolib-388172:5605715"
    implementation "curse.maven:irons-spells-n-spellbooks-855414:5618912"
    implementation "curse.maven:ars-nouveau-401955:5638541"
    implementation "curse.maven:emi-580555:5609618"
}

idea {
    module {
        downloadSources = true
        downloadJavadoc = true
    }
}